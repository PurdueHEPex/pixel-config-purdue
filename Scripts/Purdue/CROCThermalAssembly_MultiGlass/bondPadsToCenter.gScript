VERSION v2.1
######################## CROC Assembly Center of Bond Pads to True Center Script  #############################
SETLOG "LOG_bondPadsToCenter.txt"
SETERRORMODE prompt
# First specify a rough initial position for the first corner and bond pad
COPY $pos_TOP_bondpad1 {111.536,523.868,99.627}
COPY $pos_TRcorner {130.422,522.776,99.627}

# Specify Rough dimensions if they are available, otherwise set to zero and struggle
COPY $dim_object {45.196,19.598,0}  # length and width of the object {length,width,zero}
COPY $len_bondpads 41.503     # length of the bond pads

#COPY $dim_object {22.598,19.169,0}  # length and width of the object {length,width,zero}
#COPY $len_bondpads 19.698           # length of the bond pads


## Main Function ##
#GOTO @CHECKCODE
HOME
MOVENAME crocStaging 200
# Start by finding the centers of the top and bottom bond pads
       #this code was taken from the CROC Single Glass Script
CALL @FINDBONDCENTERS $pos_TOP_bondpad1 $len_bondpads -> $pos_TOP_bondpad $pos_BOT_bondpad

# Now find the center of the object by surveying the corners
CALL @FINDOBJECTCENTER $pos_TRcorner $dim_object.x $dim_object.y -> $pos_center

# Calculate and print out the translation vector (center of bonds -> true center)
CALL @CALCBONDTOCENTER $pos_TOP_bondpad $pos_BOT_bondpad $pos_center -> $vector
PRINT "The calculated vector is: %v" $vector
HOME

@CHECKCODE PASS
CALL @CALCBONDTOCENTER {2,1,10} {0,-1,8} {1.5,-1,7} -> $vector_CHECK
PRINT "The checking vector is (ignore if you aren't Cameron): %v" $vector_CHECK

END



# CALCBONDTOCENTER
# Arguments:
# $top_bond - position of the top bond pad
# $bot_bond - position of the bottom bond pad
# $true_center - position of the true center of the object
#
# Returns:
# $v_bondToCenter - vector from the center of the bond pads to the true center
#                 - assuming zero rotation
@CALCBONDTOCENTER(top_bond,bot_bond,true_center)

  # Finding the slope
  SUB $delta $top_bond $bot_bond
  INVERT $delta.x $delta.x
  MUL $m $delta.x $delta.y
  POW $m_2 $m 2

  # Finding the center
  ADD $bond_center $top_bond $bot_bond
  MUL $bond_center $bond_center {0.5,0.5,0}

  # making (1/(m^2 + 1))
  ADD $denom $m_2 1
  INVERT $denom $denom

  # making (m^2(true_center.x) - m(true_center.y) + m(bond_center.y) + bond_center.x)
  MUL $m2_true_x $m_2 $true_center.x
  MUL $m_true_y $m $true_center.y
  MUL $m_bond_y $m $bond_center.y
  SUB $numer $m2_true_x $m_true_y
  ADD $numer $numer $m_bond_y
  ADD $numer $numer $bond_center.x

  # Finding the x and y values
  MUL $intersect $numer $denom
  SUB $intersect.y $intersect.x $true_center.x
  MUL $intersect.y $intersect.y $m
  ADD $intersect.y $intersect.y $true_center.y

  # Finding the distances
  COPY $true_center.z 0
  SUB $x_dist $bond_center $intersect
  SUB $y_dist $true_center $intersect
  ABS $v_bondToCenter $x_dist
  ABS $v_bondToCenter.y $y_dist

  # Making sure the values have the right sign
  SUB $check_sign $true_center.x $bond_center.x
  SUB $check_sign.y $true_center.y $intersect.y
  ABS $norm_sign $check_sign.x
  INVERT $norm_sign $norm_sign
  ABS $norm_sign.y $check_sign.y
  INVERT $norm_sign.y $norm_sign.y
  MUL $sign $check_sign $norm_sign
  MUL $v_bondToCenter $v_bondToCenter $sign

  RETURN $v_bondToCenter


# SURVEYCORNERS
# Arguments:
# $x_corner - Approximate position of an object that you would like to know the precise position. Returns with precise position written in this location
#
# Returns:
# $x_corner - Updated position of the corner that is being looked for
@SURVEYCORNERS(x_corner)
  MOVESAFE $x_corner 100
  @SURCORNPOP VIDEO
  CHOICEPOPUP $choice "Did you correctly align the cursor with the marker?" "Yes" "No"
  GOTOIFN @SURCORNPOP $choice
  GETPOS $x_corner
  RETURN $x_corner


# FINDBONDCENTERS
# Arguments:
# $top_bond1 - rough position of a corner of the top bond pad
# $bond_len - length of the bond pads
#
# Returns:
# $top_bond - center of the top-most bond pad
# $bot_bond - cneter of the bottom-most bond pad
@FINDBONDCENTERS(top_bond1,bond_len)
  @FIND_0 CHOICEPOPUP $choice "Ready to find a corner of the top bond pad?" "Yes" "No"
  GOTOIFN @FIND_0 $choice
  CALL @SURVEYCORNERS $top_bond1 -> $top_bond1

  COPY $top_bond2 $top_bond1
  @FIND_1 CHOICEPOPUP $choice "Ready to find the opposite corner of the top bond pad?" "Yes" "No"
  GOTOIFN @FIND_1 $choice
  CALL @SURVEYCORNERS $top_bond2 -> $top_bond2

  COPY $bot_bond1 $top_bond1
  ADD $bot_bond1.y $bot_bond1.y $bond_len
  @FIND_2 CHOICEPOPUP $choice "Ready to find a corner of the bottom bond pad?" "Yes" "No"
  GOTOIFN @FIND_2 $choice
  CALL @SURVEYCORNERS $bot_bond1 -> $bot_bond1

  COPY $bot_bond2 $bot_bond1
  @FIND_3 CHOICEPOPUP $choice "Ready to find the opposite corner of the top bond pad?" "Yes" "No"
  GOTOIFN @FIND_3 $choice
  CALL @SURVEYCORNERS $bot_bond2 -> $bot_bond2

    # Getting the center of the bond pads
  ADD $top_bond $top_bond1 $top_bond2
  MUL $top_bond $top_bond {0.5,0.5,0.5}
  ADD $bot_bond $bot_bond1 $bot_bond2
  MUL $bot_bond $bot_bond {0.5,0.5,0.5}
  RETURN $top_bond $bot_bond


#FINDOBJECTCENTER
# Arguments:
#  $tr - approximate position of the top right corner
#  $len - length of what is being surveyed
#  $wid - width of what is being surveyed
#
# Returns:
#  $center - calculated position of the center based on the corners
@FINDOBJECTCENTER(tr,len,wid)
  MOVESAFE $tr 100
  @ROC_0 CHOICEPOPUP $choice "Ready to find the Top Right marker?" "Yes" "No"
  GOTOIFN @ROC_0 $choice
  CALL @SURVEYCORNERS $tr -> $tr

  COPY $br $tr
  ADD $br.y $tr.y $len
  @ROC_1 CHOICEPOPUP $choice "Ready to find the Bottom Right marker?" "Yes" "No"
  GOTOIFN @ROC_1 $choice
  CALL @SURVEYCORNERS $br -> $br

  COPY $bl $br
  SUB $bl.x $bl.x $wid
  @ROC_2 CHOICEPOPUP $choice "Ready to find the Bottom Left marker?" "Yes" "No"
  GOTOIFN @ROC_2 $choice
  CALL @SURVEYCORNERS $bl -> $bl

  COPY $tl $tr
  SUB $tl.x $tl.x $wid
  @ROC_3 CHOICEPOPUP $choice "Ready to find the Top Left marker?" "Yes" "No"
  GOTOIFN @ROC_3 $choice
  CALL @SURVEYCORNERS $tl -> $tl

  # Calculate the center of the object
  ADD $sum1 $tr $br
  ADD $sum2 $bl $tl
  ADD $sum $sum1 $sum2
  MUL $center $sum {0.25,0.25,0.25}

  RETURN $center
