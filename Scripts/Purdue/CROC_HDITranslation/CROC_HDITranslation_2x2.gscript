VERSION v2.1
######################## CROC 2x2 HDI Translation #############################
# Steps:
# 1. Prompt what station to measure the translation at
# 2. Turn on the HDI's vacuum
# 3. Prompt placement of appropriate Weight Tool on the HDI
# 4. Move the gantry to the HDI for surveillance
# 5. Prompt placement of Weight Tool bearing HDI onto the thermal mockups
# 6. Move gantry head to the module and survey for accuracy
# 7. Calculate a two points and rotations from the initial and final bond pad locations
# 8. Release vacuum of Weight Tool
# 9. Prompt replacement of Weight Tool to its original position
# 10. End program with vacuums on or off

#Initialize the system
LOADCONFIG
HOME
SETVAC gantry_head_inner 0
SETVAC gantry_head_outer 0
SETVAC croc2x2_chuck_0_tr 0
SETVAC croc2x2_chuck_0_br 0
SETVAC croc2x2_chuck_0_bl 0
SETVAC croc2x2_chuck_0_tl 0
SETVAC croc2x2_launchHDI 0

# Step 1. Prompt what station to measure the translation at
@STEP_ONE GETINTPOPUP $station "Provide the Chuck number (0-4)"
GOTOIF @DISPLAY_ERROR `$station<0`
GOTOIF @DISPLAY_ERROR `$station>4`
GOTO @VACUUM_STEP
@DISPLAY_ERROR CHOICEPOPUP $trash "You gave me a bad number >:(" "Sorry" "My bad"
GOTO @STEP_ONE

@VACUUM_STEP PASS
SETVAC "croc2x2_weight_{$station}" 0
CALL @STATIONVAC $station 0

SETLOG "LOG_HDI_Translation_CROC2X2_DataGeneration_station_{$station}.txt"
SETERRORMODE prompt

#Rough positions of the top bond pad on the CROC HDI
FLEXREAD $pos_croc2x2_hdi_TR siteGeometry.pos_croc2x2_hdi_TR
FLEXREAD $vect_translation "siteGeometry.pos_croc2x2_translation_STAT_{$station}"
# COPY $pos_final_tr {506.785710,522.195129,93.454998}
ADD $pos_final_tr $pos_croc2x2_hdi_TR $vect_translation

#Dimensions of the HDI set of bond pads
FLEXREAD $dim geometry.TFPX_CROC2X2_HDI.fid_br
MUL $dim $dim {2,2,0}

## Main Function ##

# 2. Turn on the HDI's vacuum
@STEP_TWO CHOICEPOPUP $choice "Did you place the CROC2X2 HDI on Launch HDI?" "Yes, continue" "No, ask me again"
GOTOIFN @STEP_TWO $choice
SETVAC croc2x2_launchHDI 1

# 3. Prompt placement of weight tool on the HDI
@STEP_THREE CHOICEPOPUP $choice "Have you placed Weight Tool {$station} on the HDI?" "Yes, continue" "No, ask me again"
GOTOIFN @STEP_THREE $choice
SETVAC "croc2x2_weight_{$station}" 1
WAIT 1000
SETVAC croc2x2_launchHDI 0

# 4. Move the gantry to the HDI for surveillance
@STEP_FOUR PASS
MOVENAME crocStaging 100
MOVESAFE $pos_croc2x2_hdi_TR 100
@HDI_0 CHOICEPOPUP $choice "Ready to find the top-right corner of the HDI's top-right bond pad?" "Yes" "No"
GOTOIFN @HDI_0 $choice
CALL @SURVEYCORNERS $pos_croc2x2_hdi_TR -> $pos_croc2x2_hdi_TR

ADD $pos_croc2x2_hdi_BR $pos_croc2x2_hdi_TR {0,$dim.y,0}
@HDI_1 CHOICEPOPUP $choice "Ready to find the bottom-right corner of the HDI's bottom-right bond pad?" "Yes" "No"
GOTOIFN @HDI_1 $choice
CALL @SURVEYCORNERS $pos_croc2x2_hdi_BR -> $pos_croc2x2_hdi_BR

SUB $pos_croc2x2_hdi_BL $pos_croc2x2_hdi_BR {$dim.x,0,0}
@HDI_2 CHOICEPOPUP $choice "Ready to find the bottom-left corner of the HDI's bottom-left bond pad?" "Yes" "No"
GOTOIFN @HDI_2 $choice
CALL @SURVEYCORNERS $pos_croc2x2_hdi_BL -> $pos_croc2x2_hdi_BL

SUB $pos_croc2x2_hdi_TL $pos_croc2x2_hdi_BL {0,$dim.y,0}
@HDI_3 CHOICEPOPUP $choice "Ready to find the top-left corner of the HDI's top-left bond pad?" "Yes" "No"
GOTOIFN @HDI_3 $choice
CALL @SURVEYCORNERS $pos_croc2x2_hdi_TL -> $pos_croc2x2_hdi_TL

MOVENAME crocStaging 100


# 5. Prompt placement of Weight Tool bearing HDI onto the thermal mockups
@STEP_FIVE CHOICEPOPUP $choice "Did you place the HDI on Chuck {$station}?" "Yes, continue" "No, ask me again"
GOTOIFN @STEP_FIVE $choice

CALL @STATIONVAC $station 1
WAIT 1000



# 6. Move gantry head to the module and survey for accuracy
@STEP_SIX PASS
@SURVEY_0 CHOICEPOPUP $choice "Ready to find the top-right corner of the HDI's top-right bond pad?" "Yes" "No"
GOTOIFN @HDI_0 $choice
CALL @SURVEYCORNERS $pos_final_tr -> $pos_final_tr

ADD $pos_final_br $pos_final_tr {0,$dim.y,0}
@SURVEY_1 CHOICEPOPUP $choice "Ready to find the bottom-right corner of the HDI's bottom-right bond pad?" "Yes" "No"
GOTOIFN @HDI_1 $choice
CALL @SURVEYCORNERS $pos_final_br -> $pos_final_br

SUB $pos_final_bl $pos_final_br {$dim.x,0,0}
@SURVEY_2 CHOICEPOPUP $choice "Ready to find the bottom-left corner of the HDI's bottom-left bond pad?" "Yes" "No"
GOTOIFN @HDI_2 $choice
CALL @SURVEYCORNERS $pos_final_bl -> $pos_final_bl

SUB $pos_final_tl $pos_final_bl {0,$dim.y,0}
@SURVEY_3 CHOICEPOPUP $choice "Ready to find the top-left corner of the HDI's top-left bond pad?" "Yes" "No"
GOTOIFN @HDI_3 $choice
CALL @SURVEYCORNERS $pos_final_tl -> $pos_final_tl

# 7. Calculate a two points and rotations from the initial and final bond pad locations
@STEP_SEVEN PASS
  #Initial position calculations
FIT $pos_croc2x2_hdi_initial $rot_croc2x2_hdi_initial TFPX_CROC2X2_HDI $pos_croc2x2_hdi_TR $pos_croc2x2_hdi_BR $pos_croc2x2_hdi_BL $pos_croc2x2_hdi_TL
QUAT2EULER $rot_croc2x2_hdi_initial $trash $trash1 $rot_croc2x2_hdi_initial
  #Final position calculations
FIT $pos_croc2x2_hdi_final $rot_croc2x2_hdi_final TFPX_CROC2X2_HDI $pos_final_tr $pos_final_br $pos_final_bl $pos_final_tl
QUAT2EULER $rot_croc2x2_hdi_final $trash $trash1 $rot_croc2x2_hdi_final

  #Calculate the vector from initial -> final
SUB $vect_translation $pos_croc2x2_hdi_final $pos_croc2x2_hdi_initial
#SUB $vect_translation $vect_translation {0,0,0.808} #Correction for Quad Thermal ROC
SUB $rot_translation $rot_croc2x2_hdi_final $rot_croc2x2_hdi_initial

  #Print out the data
PRINT "Station Number: %d" $station
PRINT "HDI Translation Vector: %v" $vect_translation
PRINT "HDI Translation Rotation: %f" $rot_translation

# 8. Release vacuum of Weight Tool
@STEP_EIGHT PASS
#SETVAC croc2x2_weight_0 0
SETVAC "croc2x2_weight_{$station}" 0


# 9. Prompt replacement of Weight Tool to its original position
@STEP_NINE CHOICEPOPUP $choice "Did you place Weight Tool {$station} on its original position?" "Yes, continue" "No, ask me again"
GOTOIFN @STEP_NINE $choice

# 10. End program with vacuums on or off
@STEP_TEN PASS
CHOICEPOPUP $choice "Do you want to leave the vacuums on?" "Yes, Leave them on" "No, Turn them off"
GOTOIF @STEP_END $choice

SETVAC gantry_head_inner 0
SETVAC gantry_head_outer 0
CALL @STATIONVAC $station 0
SETVAC croc2x2_launchHDI 0
# SETVAC croc2x2_weight_0 0
SETVAC "croc2x2_weight_{$station}" 0

@STEP_END HOME

END

# SURVEYCORNERS
# Arguments:
# $x_corner - Approximate position of an object that you would like to know the precise position. Returns with precise position written in this location
#
# Returns:
# $x_corner - Updated position of the corner that is being looked for
@SURVEYCORNERS(x_corner)
  MOVESAFE $x_corner 100
  @SURCORNPOP VIDEO
  CHOICEPOPUP $choice "Did you correctly align the cursor with the marker?" "Yes" "No"
  GOTOIFN @SURCORNPOP $choice
  GETPOS $x_corner
  RETURN $x_corner

# STATIONVAC
# Arguments:
#  $stat_num - station number
#  $vac_state - 1 to turn vacuums on, 0 to turn vacuums off
#
# No returns
@STATIONVAC(stat_num,vac_state)
  GOTOIF @SINGLEVAC `$stat_num>0`

  SETVAC croc2x2_chuck_0_tr $vac_state
  SETVAC croc2x2_chuck_0_br $vac_state
  SETVAC croc2x2_chuck_0_bl $vac_state
  SETVAC croc2x2_chuck_0_tl $vac_state
  RETURN

  @SINGLEVAC PASS
  SETVAC "croc2x2_chuck_{$stat_num}" $vac_state
  RETURN
